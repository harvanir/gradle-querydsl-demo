def queryDslOutputDir = file("src/main/generated")

sourceSets {
    main {
        java {
            srcDirs += queryDslOutputDir
        }
    }
}

task compileQuerydsl(type: JavaCompile, group: 'build') {
    doFirst {
        if (!queryDslOutputDir.exists()) {
            logger.info("Creating `$queryDslOutputDir` directory")

            if (!queryDslOutputDir.mkdirs()) {
                throw new InvalidUserDataException("Unable to create `$queryDslOutputDir` directory")
            }
        }
    }

    source = sourceSets.main.java
    classpath = configurations.compile
    options.compilerArgs = [
            "-proc:only",
            "-processor",
            "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = queryDslOutputDir
}

clean {
    delete queryDslOutputDir
}

compileJava.dependsOn(compileQuerydsl)